// kind=shader
//
// SPDX-FileCopyrightText: Copyright 2026 Automat Authors
// SPDX-License-Identifier: MIT
//
// Metal gear shape

uniform float iTime;

const float M_PI = 3.14159265358979323846;

float notch(float x, float left, float center, float right) {
  if (x > right || x < left) return 1;
  if (x < center)
    return smoothstep(center, left, x);
  return smoothstep(center, right, x);
}

float d_smoothstep(float edge0, float edge1, float x) {
  x = (x - edge0) / (edge1 - edge0);
  x = min(1, x);
  x = max(0, x);
  return 6 * x * (1 - x) * (edge0 - edge1);
}

float d_notch(float x, float left, float center, float right) {
  if (x > right || x < left) return 0;
  if (x < center)
    return d_smoothstep(center, left, x);
  return d_smoothstep(center, right, x);
}

vec4 main(vec2 localCoord) {
  vec2 uv = localCoord * 100; // scale coordinate system from metric, so that 1cm = 1

  // Compute h
  float angle = atan(uv.y, uv.x);
  float x = length(uv);
  float r = 0.9 + 0.1 * sin((angle + iTime) * 12);

  float h = 0.8 + 0.2 * sqrt(1 - pow(x, 2)); // rounded surface

  // Also compute the normals of the gear surface
  // Most of the formulas of the dx & dy were guessed based on looks and are probably not accurate.
  float dx = 0.2 * sin(x * M_PI / 2); // Normal along the radius
  float dy = 0; // Normal perpendicular to the radius

  float margin = 0.1;
  if (x > r - margin) {
    float mul = sqrt(1 - pow((r - margin - x) / margin, 2));
    dx = dx * mul - h * sin((r - margin - x) / margin * M_PI / 2);
    dy = h * -cos((angle + iTime) * 12) * (1 - mul); // certainly wrong but looks acceptable
    h *= mul;
  }

  if (x < 0.15) { // center hole
    float mul = sqrt(max(0, (1 - pow((x - 0.15) / 0.05, 2))));
    dx = dx * mul + h * sin((x - 0.15) / 0.05 * M_PI / 2);
    h *= mul;
  }

  { // middle groove
    float mul = 0.3 + 0.7 * notch(x, 0.25, 0.35, 0.85);
    dx = dx * mul + h * 0.7 * d_notch(x, 0.25, 0.35, 0.85);
    h *= mul;
  }

  h = max(h, 0.0);

  float alpha = h > 0 ? 1 : 0;

  vec3 normal = vec3(
      cos(angle) * dx - sin(angle) * dy,
      sin(angle) * dx + cos(angle) * dy,
      sqrt(1 - pow(dx, 2)));
  normal = normalize(normal);

  // return vec4(normal * 0.5 + 0.5, 1) * alpha;

  // Metal gear shading

  vec3 lightDir = normalize(vec3(0, 0.6, 0.8));
  vec3 viewDir = vec3(0, 0, 1);
  vec3 halfDir = normalize(lightDir + viewDir);

  // Base metal colors (warm gray/brown tones from reference)
  vec3 metalDark = vec3(0.25, 0.23, 0.22); // Dark recesses
  vec3 metalMid = vec3(0.45, 0.42, 0.40); // Mid tones
  vec3 metalLight = vec3(0.65, 0.62, 0.58); // Highlights
  vec3 metalBright = vec3(0.85, 0.82, 0.78); // Bright specular

  // Normalize the normal
  vec3 N = normalize(normal);

  // Diffuse lighting (Lambert)
  float NdotL = max(dot(N, lightDir), 0.0);
  float diffuse = NdotL * 0.6 + 0.4; // Some ambient fill

  // Specular (Blinn-Phong)
  float NdotH = max(dot(N, halfDir), 0.0);
  float specular = pow(NdotH, 32.0) * 0.5;

  // Fresnel-like rim lighting - disabled because of weird normals
  float NdotV = max(dot(N, viewDir), 0.0);
  float rim = pow(1.0 - NdotV, 2.0) * 0.3;

  // Height-based ambient occlusion
  float ao = smoothstep(-1, 1.0, h);

  // Combine base color based on height and lighting
  vec3 baseColor = mix(metalDark, metalMid, smoothstep(0.0, 0.4, h));
  baseColor = mix(baseColor, metalLight, smoothstep(0.4, 0.8, h));

  // Apply lighting
  vec3 color = baseColor * diffuse * ao;
  color += metalBright * specular;
  color += metalLight * rim;

  // TODO: implement proper antialiasing
  //       - find SDF
  //       - get pixel size & use it for alpha-blending
  alpha = min(alpha, h / 0.3);

  return vec4(color, 1.0) * alpha;
}
